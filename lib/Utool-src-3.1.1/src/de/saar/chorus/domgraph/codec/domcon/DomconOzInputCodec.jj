options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  UNICODE_INPUT = true;
}

PARSER_BEGIN(DomconOzInputCodec)

package de.saar.chorus.domgraph.codec.domcon;

import java.io.*;
import java.util.*;

import de.saar.chorus.domgraph.graph.*;
import de.saar.chorus.domgraph.codec.*;

import org.testng.annotations.ExpectedExceptions;
import org.testng.annotations.Test;


/**
 * An input codec for weakly normal dominance constraints in Oz syntax. The
 * inputs that this codec accepts are lists of Oz terms; these terms
 * represent dominance and labelling atoms.<p>
 *
 * An example input looks as follows:<p>
 * <code>[label(x f(y)) dom(y z) label(z a)]</code><p>
 *
 * The dominance constraint represented by the Oz term is read as a
 * weakly normal dominance constraint, i.e. we require that the
 * right-hand sides of dominance atoms are roots, and we assume that
 * there is an implicit inequality atom for each pair of labelled
 * variables.<p>
 *
 * The constraints accepted by this codec are compatible with the
 * old CHORUS demo. A line that starts with a percent sign is
 * interpreted as a comment.
 * 
 * @author Alexander Koller
 *
 */
@CodecMetadata(name="domcon-oz", extension=".clls")
public class DomconOzInputCodec extends InputCodec {
    private DomGraph graph;
    private NodeLabels labels;
  private List<String> varlist;
 
    @CodecConstructor
    public DomconOzInputCodec() {
	this((Reader) null);
	varlist = new ArrayList<String>();
    }
    
    public void decode(Reader inputStream, DomGraph graph, NodeLabels labels)
	throws IOException, ParserException, MalformedDomgraphException {

	this.graph = graph;
	this.labels = labels;

	graph.clear();
	labels.clear();
	varlist.clear();

	try {
	    ReInit(inputStream);
	    Input();
	} catch(Throwable e) {
//	    e.printStackTrace(System.err);
	    throw new ParserException(e);
	}
    }


    private String stripquotes(String label) {
	if( label.startsWith("\'") ) {
	    // strip off first and last character
	    return label.substring(1, label.length()-1);
	} else {
	    return label;
	}
    }
    
    

}

PARSER_END(DomconOzInputCodec)


/*
 * lexical scanner
 */
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <PERCENT_COMMENT: "%" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>  
}

TOKEN :
{
    <LABEL : "label">
 |  <DOM   : "dom">
 |  <OZATOM  : "\'" (~["\'"])* "\'"
  	   | ["a"-"z"] (["a"-"z", "A"-"Z", "0"-"9", "_"])*>

}




/*
 * grammar
 */

// start symbol: Input
void Input() :
{}
{
  // Input -> [ Atom+ ]
    "[" (Atom())+ "]"
}

void Atom() :
{
  String x,y;
  Token f;
}
{
  // Atom -> DOM ( Variable Variable )
    <DOM> "(" x=Variable() y=Variable() ")"
     { 
	 graph.addEdge(x, y, 
		       new EdgeData(EdgeType.DOMINANCE));
     }


  // Atom -> LABEL ( Variable OZATOM [ ( Varlist ) ] )
 | <LABEL> "(" x=Variable() f=<OZATOM> [ "(" Varlist() ")"  ] ")"
    {
      // INVARIANT: varlist is empty each time we start recognising
      // a labelling atom. It is filled while parsing the Varlist.

	graph.getData(x).setType(NodeType.LABELLED);
	labels.addLabel(x, stripquotes(f.image));

	for( String child : varlist ) {
	    graph.addEdge(x, child,
			  new EdgeData(EdgeType.TREE));
	}

	varlist.clear();
    }
}

void Varlist() :
{
  String t;
}
{
  // Varlist -> Variable +
    (t=Variable() {varlist.add(t);})+
}



String Variable() :
{
  Token t;
}
{
  // Variable -> OZATOM
  t=<OZATOM>
  {
    String nodename = stripquotes(t.image);

    if( !graph.hasNode(nodename) ) {
      graph.addNode(nodename, new NodeData(NodeType.UNLABELLED));
    }

    return nodename;
  }
}
